plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    alias libs.plugins.kotlin.serialization
    alias libs.plugins.ksp
    id 'jacoco'
}

 android {
    namespace = 'com.example.iurankomplek'
    compileSdk = 34

    defaultConfig {
        applicationId = "com.example.blokp"
        minSdk = 24
        targetSdk = 34
        versionCode = 1
        versionName = "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        def apiSpreadsheetId = project.hasProperty('API_SPREADSHEET_ID') ? project.property('API_SPREADSHEET_ID') : System.getenv('API_SPREADSHEET_ID')
        if (apiSpreadsheetId == null) {
            logger.warn("API_SPREADSHEET_ID not configured. Set it in local.properties or as environment variable.")
        }
        buildConfigField "String", "API_SPREADSHEET_ID", "\"${apiSpreadsheetId ?: ''}\""

        def productionBaseUrl = project.hasProperty('PRODUCTION_BASE_URL') ? project.property('PRODUCTION_BASE_URL') : System.getenv('PRODUCTION_BASE_URL')
        if (productionBaseUrl == null) {
            logger.warn("PRODUCTION_BASE_URL not configured. Using default value: https://api.apispreadsheets.com/data/")
        }
        buildConfigField "String", "PRODUCTION_BASE_URL", "\"${productionBaseUrl ?: 'https://api.apispreadsheets.com/data/'}\""

        def mockBaseUrl = project.hasProperty('MOCK_BASE_URL') ? project.property('MOCK_BASE_URL') : System.getenv('MOCK_BASE_URL')
        if (mockBaseUrl == null) {
            logger.warn("MOCK_BASE_URL not configured. Using default value: https://api-mock:5000/data/")
        }
        buildConfigField "String", "MOCK_BASE_URL", "\"${mockBaseUrl ?: 'https://api-mock:5000/data/'}\""

        def certificatePinner = project.hasProperty('CERTIFICATE_PINNER') ? project.property('CERTIFICATE_PINNER') : System.getenv('CERTIFICATE_PINNER')
        if (certificatePinner == null) {
            logger.warn("CERTIFICATE_PINNER not configured. Using default pins.")
        }
        buildConfigField "String", "CERTIFICATE_PINNER", "\"${certificatePinner ?: 'sha256/PIdO5FV9mQyEclv5rMC4oGNTya7Q9S5/Sn1KTWpQov0=;sha256/G9LNNAql897egYsabashkzUCTEJkWBzgoEtk8X/678c=;sha256/++MBgDH5WGvL9Bcn5Be30cRcL0f5O+NyoXuWtQdX1aI='}\""
    }

    configurations.all {
        resolutionStrategy {
            force "org.jetbrains.kotlin:kotlin-stdlib:${libs.versions.kotlin.get()}"
            force "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${libs.versions.kotlin.get()}"
            force "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${libs.versions.kotlin.get()}"
        }
    }
    
    buildTypes {
        debug {
            testCoverageEnabled = true
        }
        release {
            minifyEnabled = true
            shrinkResources = true
            proguardFiles = [getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro']
        }
    }
    buildFeatures {
        viewBinding = true
        buildConfig = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += [
            '-opt-in=kotlin.RequiresOptIn'
        ]
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }

    lint {
        abortOnError = false
        checkReleaseBuilds = true
        xmlReport = true
        htmlReport = true
        disable 'NotificationPermission'
    }
}

dependencies {

      implementation libs.androidx.core.ktx
      implementation libs.androidx.appcompat
      implementation libs.material
      implementation libs.androidx.constraintlayout
    
    // Testing dependencies
    testImplementation libs.junit
    testImplementation libs.mockito.core
    testImplementation libs.mockito.kotlin
    testImplementation libs.arch.core.testing
    testImplementation libs.kotlinx.coroutines.test
    testImplementation libs.robolectric
    testImplementation libs.mockwebserver
    
    androidTestImplementation libs.androidx.test.ext.junit
    androidTestImplementation libs.espresso.core
    androidTestImplementation libs.espresso.contrib
    androidTestImplementation libs.androidx.test.runner
    androidTestImplementation libs.androidx.test.rules
    androidTestImplementation libs.mockwebserver
    androidTestImplementation libs.room.testing
    androidTestImplementation libs.kotlinx.coroutines.test
    androidTestImplementation libs.arch.core.testing
    
    implementation libs.androidx.recyclerview
    implementation libs.swiperefreshlayout
    implementation libs.glide

    implementation libs.gson

    implementation libs.retrofit
    implementation libs.retrofit.converter.gson

    implementation libs.okhttp3.logging.interceptor
    debugImplementation libs.chucker
    
    // Room database
    implementation libs.room.runtime
    implementation libs.room.ktx
    ksp libs.room.compiler
    
    // Coroutines
    implementation libs.kotlinx.coroutines.android
    
    // Lifecycle
    implementation libs.lifecycle.viewmodel.ktx
    implementation libs.lifecycle.runtime.ktx
    
    // Kotlin Serialization (needed for WebhookReceiver)
    implementation libs.kotlinx.serialization.json
}