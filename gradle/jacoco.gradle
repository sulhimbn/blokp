apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.8.11"
}

tasks.register('jacocoTestReport', JacocoReport) {
    dependsOn 'testDebugUnitTest'

    reports {
        xml.required.set(true)
        html.required.set(true)
        csv.required.set(false)
    }

    def fileFilter = [
        '**/R.class',
        '**/R$*.class',
        '**/BuildConfig.*',
        '**/Manifest*.*',
        '**/*Test*.*',
        'android/**/*.*',
        '**/di/**',
        '**/databinding/**',
        '**/android/databinding/**',
        '**/androidx/databinding/**',
        '**/*_HiltModules_*',
        '**/*_HiltComponents_*',
        '**/*_Factory.*',
        '**/*_MembersInjector.*',
        '**/*_Provide*Factory.*',
        '**/BR.*',
        '**/DataBindingTrigger*',
        '**/generated/**'
    ]

    def debugTree = fileTree(dir: "${buildDir}/tmp/kotlin-classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories.setFrom(files([mainSrc]))
    classDirectories.setFrom(files([debugTree]))
    executionData.setFrom(fileTree(dir: buildDir, includes: [
        'outputs/unit_test_code_coverage/debugUnitTest/testDebugUnitTest.exec',
        'jacoco/testDebugUnitTest.exec'
    ]))
}

tasks.register('jacocoTestCoverageVerification', JacocoCoverageVerification) {
    dependsOn 'jacocoTestReport'

    violationRules {
        rule {
            limit {
                minimum = 0.70
            }
        }
    }
    def fileFilter = [
        '**/R.class',
        '**/R$*.class',
        '**/BuildConfig.*',
        '**/Manifest*.*',
        '**/*Test*.*',
        'android/**/*.*',
        '**/di/**',
        '**/databinding/**',
        '**/android/databinding/**',
        '**/androidx/databinding/**',
        '**/*_HiltModules_*',
        '**/*_HiltComponents_*',
        '**/*_Factory.*',
        '**/*_MembersInjector.*',
        '**/*_Provide*Factory.*',
        '**/BR.*',
        '**/DataBindingTrigger*',
        '**/generated/**'
    ]

    def debugTree = fileTree(dir: "${buildDir}/tmp/kotlin-classes/debug", excludes: fileFilter)

    sourceDirectories.setFrom(files("${project.projectDir}/src/main/java"))
    classDirectories.setFrom(files([debugTree]))
    executionData.setFrom(fileTree(dir: buildDir, includes: [
        'outputs/unit_test_code_coverage/debugUnitTest/testDebugUnitTest.exec',
        'jacoco/testDebugUnitTest.exec'
    ]))
}

gradle.projectsEvaluated {
    tasks.withType(Test).configureEach {
        jacoco.includeNoLocationClasses = true
        jacoco.excludes = ['jdk.internal.*']
    }
}
